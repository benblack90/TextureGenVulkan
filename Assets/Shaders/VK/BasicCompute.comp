//GLSL version to use
#version 460

//size of a workgroup for compute
layout (local_size_x = 16, local_size_y = 16) in;

//descriptor bindings for the pipeline
layout(rgba8,set = 0, binding = 0) uniform image2D image;

layout(push_constant) uniform pConsts
{
	vec3 positionOffset;
    int[6] LoD;
};

const float PI = 3.14159265359;

layout(set = 0, binding = 2) buffer permutations
{
    vec4[513] perms;
};

vec2 GetConstVec(int x)
{
  //biasing the noise to be horizontal  
  
    int hash = x % 6;
    if(hash == 0) return vec2(0.0, 1.4142);
    if(hash == 1) return vec2 (0.0, -1.4142);    
    if(hash == 2) return vec2(0.2455732529,1.392715124);
    if(hash == 3) return vec2(-0.2455732529, 1.392715124);
    if(hash == 4) return vec2(0.2455732529, -1.392715124);
    if(hash == 5) return vec2(-0.2455732529, -1.392715124);

}

float Ease(float val)
{
    return val * val * val * (val * (val * 6.0f - 15.0f) + 10.0f); 
}

//Calculates Perlin using pre-created const vecs sent over in uniform
float Perlin2d(float xx, float yy)
{
    
    int x = int(xx);
    int y = int(yy);
    float freqX = xx - int(xx);
    float freqY = yy - int(yy);  

    vec2 bottomLeft = vec2(freqX, freqY);
    vec2 bottomRight = vec2(freqX - 1.0f, freqY);
    vec2 topLeft = vec2(freqX, freqY - 1.0f);
    vec2 topRight = vec2(freqX - 1.0f, freqY - 1.0f);

    vec4 cvBottomLeft = perms[int(perms[x].z) + y];
    vec4 cvBottomRight = perms[int(perms[x + 1].z) + y];
    vec4 cvTopLeft = perms[int(perms[x].z) + y + 1];
    vec4 cvTopRight = perms[int(perms[x + 1].z) + y + 1];

    float dotBotL = dot(bottomLeft, vec2(cvBottomLeft));
    float dotBotR = dot(bottomRight, vec2(cvBottomRight));
    float dotTopL = dot(topLeft, vec2(cvTopLeft));
    float dotTopR = dot(topRight, vec2(cvTopRight));
        
    float u = Ease(freqX);
    float v = Ease(freqY);
    return (mix(    mix(dotBotL, dotTopL, v),  mix(dotBotR, dotTopR, v), u));

}

//calculates perlin by hashing permuation number GPU side into a constant vector
float Perlin2d2(float xx, float yy)
{
    
    int x = int(xx);
    int y = int(yy);
    float freqX = xx - int(xx);
    float freqY = yy - int(yy);  

    vec2 bottomLeft = vec2(freqX, freqY);
    vec2 bottomRight = vec2(freqX - 1.0f, freqY);
    vec2 topLeft = vec2(freqX, freqY - 1.0f);
    vec2 topRight = vec2(freqX - 1.0f, freqY - 1.0f);

    vec4 cvBottomLeft = perms[int(perms[x].z) + y];
    vec4 cvBottomRight = perms[int(perms[x + 1].z) + y];
    vec4 cvTopLeft = perms[int(perms[x].z) + y + 1];
    vec4 cvTopRight = perms[int(perms[x + 1].z) + y + 1];

    float dotBotL = dot(bottomLeft, GetConstVec(int(cvBottomLeft.z)));
    float dotBotR = dot(bottomRight, GetConstVec(int(cvBottomRight.z)));
    float dotTopL = dot(topLeft, GetConstVec(int(cvTopLeft.z)));
    float dotTopR = dot(topRight, GetConstVec(int(cvTopRight.z)));
        
    float u = Ease(freqX);
    float v = Ease(freqY);
    return (mix(    mix(dotBotL, dotTopL, v),  mix(dotBotR, dotTopR, v), u));

}


float fracBrownMotion(vec2 coords, int octaves)
{
    float result = 0.0f;
    float gain = 0.5f;
    float freq = 1.0f;
    float amplitude = 1.0f;

    for(int i = 0; i < octaves; i++)
    {
        result += amplitude * (Perlin2d(coords.x * freq, coords.y * freq));
        freq *= 2.0f;
        amplitude *= gain;
    } 
    return (result + 1) / 2;
}

void main() 
{
    ivec2 texelCoord = ivec2(gl_GlobalInvocationID.xy);
    vec2 coordF = texelCoord;
    float freq = 0.01;
    coordF *= freq;
	ivec2 size = imageSize(image);
    float warpFactor = 1.0f + perms[512].z;
    float warping = fracBrownMotion(coordF * warpFactor, LoD[0]);
    float warpingOff = fracBrownMotion(vec2(coordF.x * warpFactor + positionOffset.x * 0.1,coordF.y * warpFactor), LoD[1]);
    float warpingOff2 = fracBrownMotion(vec2(coordF.x * warpFactor + positionOffset.x * 0.15,coordF.y * warpFactor), LoD[2]);
    float colour1 = fracBrownMotion(vec2(warpingOff, coordF.y + warping),LoD[3]);
    float colour2 = fracBrownMotion(vec2(warpingOff2, abs(coordF.y - warping)),LoD[4]);
    float bassDetail = fracBrownMotion(vec2(warping, abs(coordF.y + warping * perms[512].z)),LoD[5]);
    vec3 bass = vec3(0.369, 0.082, 0.0082);
    vec3 superBass = bass * bass;
    vec3 yel = vec3(0.741, 0.643, 0.435);
    vec3 orng = vec3(0.639, 0.286, 0.137);
   
   bass = mix(superBass, bass, smoothstep(0.0,2.0,bassDetail));
   vec3  col = mix(orng, bass, smoothstep(0.0,1.0,colour1));  
   col = mix(col, yel, smoothstep(0.45,1.0,colour2));
   col = mix(superBass, col, smoothstep(0.0, perms[512].y,bassDetail));
   
   
   vec4 colour = vec4(col,1);
    
   imageStore(image, texelCoord, colour);
}